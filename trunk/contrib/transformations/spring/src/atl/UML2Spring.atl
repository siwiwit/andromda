module UML2Spring;

create OUT: Spring from IN: UML, CONFIG: Configuration;

uses UMLHelpers;

-- Transform UML classes that have the stereotype "Service"
-- to Spring services.
rule UMLServiceToSpringService {
	from class : UML!Class (class.hasStereotype('Service'))
	to out : Spring!Service (
		name <- class.getFullyQualifiedName(),
		methods <- class.feature->select (f|f.oclIsTypeOf(UML!Operation))
	)
}

-- Transform UML operations to 3GL methods.

rule UMLOpTo3GLMethod {
	from method : UML!Operation
	to out : Spring!Method (
		name <- method.name ,
		returnType <- method.parameter->select(x|x.kind=#pdk_return)->asSequence()->first().type,
		parameters <- method.parameter->select(x|x.kind<>#pdk_return)->asSequence()
	)
}


-- Transform UML parameters to 3GL parameters.
rule UMLParameterTo3GLParameter {
 	from param : UML!Parameter (param.kind <> #pdk_return)
 	to out : Spring!Parameter (
 		name <- param.name ,
 		type <- param.type
 	)
}

rule UMLDatatypeTo3GLDatatype {
	from type : UML!DataType
	to out : Spring!Type (
		name <- type.name
	)
}


